plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'kotlin-kapt'
}

android {
    compileSdk 33

    defaultConfig {
        applicationId "ru.baccasoft.zadachnik"
        minSdk 26
        targetSdk 33
        versionCode 2
        versionName "1.0_beta"
        setProperty("archivesBaseName", "zadachnik-v$versionName-$versionCode-" + getDate())

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    signingConfigs {
        // We use a bundled debug keystore, to allow debug builds from CI to be upgradable
        named("debug") {
            storeFile = rootProject.file("debug.keystore")
            storePassword = "android"
            keyAlias = "androiddebugkey"
            keyPassword = "android"
        }
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.getByName("debug")
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    productFlavors{
        prod {
            buildConfigField "String", "API_URL", "\"https://app.tm24.ru/\""
            dimension "default"
        }
        _test {
            buildConfigField "String", "API_URL", "\"https://zadachnik-test.baccasoft.ru/\""
            applicationIdSuffix ".test"
            dimension "default"
        }
        dev {
            buildConfigField "String", "API_URL", "\"https://zadachnik-dev.baccasoft.ru/\""
            applicationIdSuffix ".dev"
            dimension "default"
        }
        httpTest {
            initWith _test
            buildConfigField "String", "API_URL", "\"http://zadachnik-test.baccasoft.ru/\""
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = getFileName(output.outputFile.name, variant.versionName)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs += [
                '-Xjvm-default=enable',
                '-opt-in=kotlin.RequiresOptIn'
        ]
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

def getDate(){
    return new Date().format('MMddHHmm')
}

def getFileName(oldFileNmae, version) {
    def cmd = "git rev-parse --short HEAD"
    def proc = cmd.execute()
    def commit = proc.text.trim()
    def appName = oldFileNmae.replace(".apk","")
    return appName + "-" + commit + ".apk";
}



dependencies {

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation "androidx.compose.ui:ui:1.3.3"
    implementation "androidx.compose.material:material:1.3.1"
    implementation "androidx.compose.ui:ui-tooling-preview:1.3.3"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'androidx.navigation:navigation-compose:2.5.3'
    //System TopBar
    implementation "com.google.accompanist:accompanist-systemuicontroller:0.28.0"
    //Gson
    implementation 'com.google.code.gson:gson:2.8.9'
    //Hilt
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-android-compiler:2.44"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0'
    //Внимание! версию alpha02 не ставить! билд падает, куча ошибок + студия багуется. Исопльзовать только 01
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.6.0-alpha04'
    implementation 'com.google.android.material:material:1.7.0'


    // Fragment/VM
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    implementation "androidx.fragment:fragment-ktx:1.5.5"

    //retorfit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.7.2"
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'

    //paging
    implementation 'androidx.paging:paging-compose:1.0.0-alpha17'

    //accompanist
    implementation "com.google.accompanist:accompanist-swiperefresh:0.28.0"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.3.3"
    debugImplementation "androidx.compose.ui:ui-tooling:1.3.3"
    debugImplementation "androidx.compose.ui:ui-test-manifest:1.3.3"
    implementation "androidx.compose.runtime:runtime-livedata:1.3.3"

    //firebase
    implementation platform('com.google.firebase:firebase-bom:31.1.1')
    implementation 'com.google.firebase:firebase-analytics-ktx:21.2.0'
    implementation 'com.google.firebase:firebase-messaging-ktx:23.1.1'

    //логирование
    implementation 'com.jakewharton.timber:timber:5.0.1'

    //ошибки зависания
    implementation 'com.github.anrwatchdog:anrwatchdog:1.4.0'

    //joda
    implementation 'joda-time:joda-time:2.12.2'

    //libPhoneNumber
    implementation 'com.googlecode.libphonenumber:libphonenumber:8.2.0'

}